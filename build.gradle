plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

version = project.mod_version
group = project.maven_group

def mcVersion = stonecutter.current.version
def mcDep = project.supported_mc_versions.toString()

version = "${property('mod_version')}-${property('jarname_target')}"

base {
	archivesName = project.archives_base_name
}

loom {
	runConfigs.all {
		ideConfigGenerated true // Run configurations are not created for subprojects by default
		runDir "../../run" // Use a shared run folder and create separate worlds
	}
	configurations.all {
		resolutionStrategy {
			force("net.fabricmc:fabric-loader:$loader_version")
		}
	}
	mixin {
		defaultRefmapName.set("pmmsc-refmap.json")
	}
}

repositories {
	maven { url "https://www.cursemaven.com" }
	maven { url 'https://jitpack.io' }
}

fabricApi {
	configureDataGeneration {
		client = true
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Conditional Mixin
	modImplementation 'com.github.Fallen-Breath.conditional-mixin:conditional-mixin-fabric:0.6.4'
	include 'com.github.Fallen-Breath.conditional-mixin:conditional-mixin-fabric:0.6.4' // Bundle into mod jar

	// Config library
	modImplementation "curse.maven:supermartijn642s-config-lib-438332:${supermartijn642_config_library_file}"
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version,
				'mcdep': project.property('supported_mc_versions'),
				'javadep': stonecutter.eval(mcVersion, ">=1.20.5") ? '>=21' : '>=17'
	}

	filesMatching("pmmsc.mixins.json5") {
		expand "refmap": "pmmsc-refmap.json"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = stonecutter.eval(mcVersion, ">=1.20.5")
			? 21 : 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	def java = stonecutter.eval(mcVersion, ">=1.20.5")
			? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
	sourceCompatibility = java
	targetCompatibility = java
}

tasks.named('sourcesJar') {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}


tasks.register("buildAndCollect", Copy) {
	group = "build"

	dependsOn("remapJar") // Important: make sure remapJar has run

	from {
		tasks.named("remapJar").get().archiveFile // âœ… No `.get()` after archiveFile!
	}
	into(rootProject.layout.buildDirectory.dir("libs/${version}"))
}

publishMods {
	def mod = project.mod_version
	def mc = stonecutter.current.version
	version = "${mod}+${mc}"
	changelog = "\n- Shiny new UI to configure Max Speed with \n- Removed old scroll wheel configuration \n- new server configuration can be found in config/pmmsc-PerMinecartMaxSpeedCustomiser-Common.toml"
	type = STABLE

	file = remapJar.archiveFile
	modLoaders.add("fabric")

	modrinth {
		projectId = "GJPp630M"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersions.add(mc)
		requires {
			id = "P7dR8mSH"
		}
        optional {
            id = "LN9BxssP"
        }
	}

//	println(">>> Publishing to tag: v${mod}")
//	github {
//		accessToken = providers.environmentVariable("GITHUB_TOKEN")
//		repository = "aullisia/Per-Minecart-Max-Speed-Customiser"
//		commitish = "master"
//		tagName = "v${mod}"
//		allowEmptyFiles = true
//	}
}